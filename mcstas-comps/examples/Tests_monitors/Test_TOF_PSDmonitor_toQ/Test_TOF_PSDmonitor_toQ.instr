/*******************************************************************************
* Instrument: Test_TOF_PSDmonitor_toQ
*
* %I
* Written by: Peter Willendrup
* Date: 20251207
* Origin: ESS
* %INSTRUMENT_SITE: Tests_monitors
*
* Test instrument for  monitor TOF_PSDmonitor_toQ (Rob Dalgliesh)
*
* %D
* Rudimentary test-instrument for monitor TOF_PSDmonitor_toQ developed by Rob Dalgliesh, ISIS.
*
* The tested monitor is a 1-dimensional Q-monitor that calculates Q from "measured" time-of-flight,
* i.e. not from particle velocity parameters.
*
* %Example: theta=0.91
*
* %P
* theta: [degs] Scattering angle
*
* %L
* <reference/HTML link>
*
* %E
*******************************************************************************/
DEFINE INSTRUMENT Test_TOF_PSDmonitor_toQ(theta=0.91)

DECLARE
%{
%}

USERVARS
%{
  double kix;
  double kiy;
  double kiz;
  double kfx;
  double kfy;
  double kfz;
  double qx;
  double qy;
  double qz;
  double Q;
  int measured;
%}

INITIALIZE
%{
%}

TRACE

COMPONENT origin = Progress_bar()
AT (0, 0, 0) RELATIVE ABSOLUTE

// insert components here (e.g. Insert -> Source -> ...)

COMPONENT Source = Source_simple(
    yheight=0.1, 
    xwidth=0.1, 
    target_index=2, 
    focus_xw=0.1, 
    focus_yh=0.002, 
    lambda0=5, 
    dlambda=4.99)
AT (0, 0, 0) RELATIVE PREVIOUS
EXTEND %{
	// 50 mu-s pulse
	t=50e-6*rand01();
	kix=vx; kiy=vy; kiz=vz;
        double Ki=V2K*sqrt((vx*vx)+(vy*vy)+(vz*vz));
        NORM(kix,kiy,kiz);
        kix=Ki*kix; kiy=Ki*kiy; kiz=Ki*kiz; 
%}

COMPONENT RotDown=Arm()
AT (0,0,0) RELATIVE Source
ROTATED (theta,0,0) RELATIVE Source

SPLIT 100 COMPONENT Vanadium = Incoherent(
    xwidth=0.1, 
    yheight=0.001,
    zdepth=0.1,
    focus_xw=0.5, 
    focus_yh=0.5, 
    target_index=2)
AT (0, 0, 50) RELATIVE RotDown
ROTATED (0,0,0) RELATIVE Source
EXTEND %{
     kfx=vx; kfy=vy; kfz=vz;
     double Kf=V2K*sqrt((vx*vx)+(vy*vy)+(vz*vz));
     NORM(kfx,kfy,kfz);
     kfx=Kf*kix; kfy=Kf*kfy; kfz=Kf*kfz;
     Q=sqrt((kix-kfx)*(kix-kfx)+(kiy-kfy)*(kiy-kfy)+(kiz-kfz)*(kiz-kfz));
%}	

COMPONENT DetArm = Arm()
AT (0,0,0) RELATIVE Vanadium
ROTATED (-theta,0,0) RELATIVE Vanadium
  
COMPONENT Q_measured = TOF_PSDmonitor_toQ(xwidth=0.5, yheight=0.5, 
           ny=90, nqbin=100, tmin=0.0, tmax=1000000, filename="Q_measured", L1=50, L2=3, qmin=1, qmax=15, detTheta=theta)
  AT (0,0,3) RELATIVE DetArm
  EXTEND %{
    measured=SCATTERED;
  %}

COMPONENT Q_virtual = Monitor_nD(filename="Q_virtual",xwidth=0.5, yheight=0.5, user1="Q", username1="Q / [AA^-1]",
 								options="parallel, previous, user1 limits=[1 15] bins=100")
  WHEN (measured) AT (0,0,0.001) RELATIVE Q_measured

FINALLY
%{
%}

END
